#!rsc by RouterOS
# RouterOS script: check-lte-firmware-upgrade
# Copyright (c) 2018-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# check for LTE firmware upgrade, send notification
# https://git.eworm.de/cgit/routeros-scripts/about/doc/check-lte-firmware-upgrade.md

:local 0 "check-lte-firmware-upgrade";
:global GlobalFunctionsReady;
:while ($GlobalFunctionsReady != true) do={ :delay 500ms; }

:global Identity;
:global SentLteFirmwareUpgradeNotification;

:global CharacterReplace;
:global LogPrintExit2;
:global SendNotification;
:global SymbolForNotification;

:foreach Interface in=[ / interface lte find ] do={
  :local IntName [ / interface lte get $Interface name ];
  :do {
    :local Firmware [ / interface lte firmware-upgrade $Interface once as-value ];

    :if ($SentLteFirmwareUpgradeNotification = ($Firmware->"latest")) do={
      $LogPrintExit2 debug $0 ("Already sent the LTE firmware upgrade notification for version " . \
        ($Firmware->"latest") . ".") false;
    } else={
      :if (($Firmware->"installed") != ($Firmware->"latest")) do={
        :local Info [ / interface lte info $Interface once as-value ];
        $SendNotification ([ $SymbolForNotification "sparkles" ] . "LTE firmware upgrade") \
          ("A new firmware version " . ($Firmware->"latest") . " is available for " . \
          "LTE interface " . $IntName . " on " . $Identity . ".\n\n" . \
          "Interface: " . [ $CharacterReplace ($Info->"manufacturer" . " " . $Info->"model") ("\"") "" ] . "\n" . \
          "Installed: " . ($Firmware->"installed") . "\n" . \
          "Available: " . ($Firmware->"latest")) "" "true";
        :set SentLteFirmwareUpgradeNotification ($Firmware->"latest");
      }
    }
  } on-error={
    $LogPrintExit2 debug $0 ("Could not get latest LTE firmware version for interface " . \
      $IntName . ".") false;
  }
}
