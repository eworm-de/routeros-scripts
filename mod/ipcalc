#!rsc by RouterOS
# RouterOS script: mod/ipcalc
# Copyright (c) 2020-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md

:global IPCalc;
:global IPCalcReturn;

# print netmask, network, min host, max host and broadcast
:set IPCalc do={
  :local Input [ :tostr $1 ];

  :global IPCalcReturn;

  :local Values [ $IPCalcReturn $1 ];

  :put ( \
    "Address:   " . $Values->"address" . "\n\r" . \
    "Netmask:   " . $Values->"netmask" . "\n\r" . \
    "Network:   " . $Values->"network" . "\n\r" . \
    "HostMin:   " . $Values->"hostmin" . "\n\r" . \
    "HostMax:   " . $Values->"hostmax" . "\n\r" . \
    "Broadcast: " . $Values->"broadcast");
}

# calculate and return netmask, network, min host, max host and broadcast
:set IPCalcReturn do={
  :local Input [ :tostr $1 ];
  :local Address [ :toip [ :pick $Input 0 [ :find $Input "/" ] ] ];
  :local Bits [ :tonum [ :pick $Input ([ :find $Input "/" ] + 1) [ :len $Input ] ] ];
  :local Mask ((255.255.255.255 << (32 - $Bits)) & 255.255.255.255);

  :local Return {
    "address"=$Address;
    "netmask"=$Mask;
    "networkaddress"=($Address & $Mask);
    "networkbits"=$Bits;
    "network"=(($Address & $Mask) . "/" . $Bits);
    "hostmin"=(($Address & $Mask) | 0.0.0.1);
    "hostmax"=(($Address | ~$Mask) ^ 0.0.0.1);
    "broadcast"=($Address | ~$Mask);
  }

  :return $Return;
}

