#!rsc by RouterOS
# RouterOS script: sms-forward
# Copyright (c) 2013-2022 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# forward SMS to e-mail
# https://git.eworm.de/cgit/routeros-scripts/about/doc/sms-forward.md

:local 0 "sms-forward";
:global GlobalFunctionsReady;
:while ($GlobalFunctionsReady != true) do={ :delay 500ms; }

:global Identity;

:global IfThenElse;
:global LogPrintExit2;
:global ScriptLock;
:global SendNotification2;
:global SymbolForNotification;
:global WaitFullyConnected;
:global ValidateSyntax;
:global SmsForwardHooks;

:if ([ :typeof $SmsForwardHooks ] != "array") do={
  :global SmsForwardHooks ({});
}

$ScriptLock $0;

:if ([ /tool/sms/get receive-enabled ] = false) do={
  $LogPrintExit2 warning $0 ("Receiving of SMS is not enabled.") true;
}

$WaitFullyConnected;

:local Settings [ /tool/sms/get ];

# forward SMS in a loop
:while ([ :len [ /tool/sms/inbox/find ] ] > 0) do={
  :local Phone [ /tool/sms/inbox/get ([ find ]->0) phone ];
  :local Messages "";
  :local Delete ({});

  :foreach Sms in=[ /tool/sms/inbox/find where phone=$Phone ] do={
    :local SmsVal [ /tool/sms/inbox/get $Sms ];

    :if ($Phone = $Settings->"allowed-number" && \
        ($SmsVal->"message")~("^:cmd " . $Settings->"secret" . " script ")) do={
      $LogPrintExit2 debug $0 ("Removing SMS, which started a script.") false;
      /tool/sms/inbox/remove $Sms;
    } else={
      :local processed false;
      $LogPrintExit2 debug $0 ("Processing message:" . $SmsVal->"message" . " " . $Phone) false;
      :foreach Hook in=$SmsForwardHooks do={
        $LogPrintExit2 debug $0 ("Checking SMS forward hook '" . $Hook . "' on message: '" . $SmsVal->"message" . "'") false;
        :local Code ($Hook->"command");
        :local Regex ($Hook->"match");
        :local AllowedNumber ($Hook->"allowed-number");
        :if ($processed = false && $Phone~$AllowedNumber && ($SmsVal->"message")~$Regex) do={
          :if ([ $ValidateSyntax $Code ] = true) do={
            :log info ("Acting on SMS forward hook '" . $Regex . "': " . $Code);
            :delay 1s;
            [ :parse $Code ];
            :log info ("Finished SMS forward hook '" . $Regex . "': " . $Code);
          } else={
            $LogPrintExit2 warning $0 ("The code for forward hook '" . $Regex . "': '" . $Code . "' failed syntax validation!") false;
          }
          # $LogPrintExit2 debug $0 ("Removing SMS, which started a forward script.") false;
          # /tool/sms/inbox/remove $Sms;
          :set processed true;
        }
      }
      :set Messages ($Messages . "\n\nOn " . $SmsVal->"timestamp" . \
          " type " . $SmsVal->"type" . ":\n" . $SmsVal->"message");
      :set Delete ($Delete, $Sms);
    }
  }

  :if ([ :len $Messages ] > 0) do={
    :local Count [ :len $Delete ];
    $SendNotification2 ({ origin=$0; \
      subject=([ $SymbolForNotification "incoming-envelope" ] . "SMS Forwarding from " . $Phone); \
      message=("Received " . [ $IfThenElse ($Count = 1) "this message" ("these " . $Count . " messages") ] . \
        " by " . $Identity . " from " . $Phone . ":" . $Messages) });
    :foreach Sms in=$Delete do={
      /tool/sms/inbox/remove $Sms;
    }
  }
}
