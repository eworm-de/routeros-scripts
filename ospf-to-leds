#!rsc by RouterOS
# RouterOS script: ospf-to-leds
# Copyright (c) 2020-2022 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# visualize ospf instance state via leds
# https://git.eworm.de/cgit/routeros-scripts/about/doc/ospf-to-leds.md

:local 0 "ospf-to-leds";
:global GlobalFunctionsReady;
:while ($GlobalFunctionsReady != true) do={ :delay 500ms; }

:global LogPrintExit2;
:global ParseKeyValueStore;

:foreach Instance in=[ / routing ospf instance find where comment~"^ospf-to-leds," ] do={
  :local InstanceVal [ / routing ospf instance get $Instance ];
  :local LED ([ $ParseKeyValueStore ($InstanceVal->"comment") ]->"leds");
  :local LEDType [ / system leds get [ find where leds=$LED ] type ];

  :if ($InstanceVal->"state" = "running" && $LEDType = "off") do={
    $LogPrintExit2 info $0 ("OSPF instance " . $InstanceVal->"name" . " is running, led on!") false;
    / system leds set type=on [ find where leds=$LED ];
  }
  :if ($InstanceVal->"state" = "down" && $LEDType = "on") do={
    $LogPrintExit2 info $0 ("OSPF instance " . $InstanceVal->"name" . " is down, led off!") false;
    / system leds set type=off [ find where leds=$LED ];
  }
}
